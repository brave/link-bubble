buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
        classpath 'io.fabric.tools:gradle:1.19.0'
    }
}

// Manifest version information!
def versionMajor = 1
def versionMinor = 9
def versionPatch = 8
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

apply plugin:'com.android.application'
apply plugin: 'io.fabric'

boolean isRelease = false;
for (int i = 0; i < project.gradle.startParameter.taskNames.size(); i++) {
    String item = project.gradle.startParameter.taskNames.get(i);
    if (item.contains("Release")) {
        isRelease = true;
    }
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 22

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        ndk {
            ldLibs "log"
        }
    }

    productFlavors {
        playstore {
            applicationId='com.brave.playstore'

            buildConfigField "String", "STORE_PACKAGE", "\"com.android.vending\""
            buildConfigField "String", "STORE_URL_PREFIX", "\"http://play.google.com/store/apps/details?id=\""
            buildConfigField "String", "STORE_URL", "\"http://play.google.com/store/apps/details?id=com.linkbubble.playstore\""
            buildConfigField "String", "STORE_MY_OTHER_APPS_URL", "\"https://play.google.com/store/apps/developer?id=Link+Bubble\""
            buildConfigField "String", "TAP_PATH_PACKAGE_NAME", "\"com.digitalashes.tappath.playstore\""
        }
    }

    signingConfigs {
        release {
            if(isRelease) {
                storeFile file(System.getenv("LINK_BUBBLE_KEYSTORE_LOCATION"))
                storePassword System.getenv("LINK_BUBBLE_KEYSTORE_PASSWORD")
                keyAlias System.getenv("LINK_BUBBLE_KEY_ALIAS")
                keyPassword System.getenv("LINK_BUBBLE_KEY_PASSWORD")
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
            debuggable true
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        lintConfig file("lint.xml")
    }
}

dependencies {
    compile 'com.android.support:support-v4:22.2.0'
    compile "com.android.support:appcompat-v7:22.2.0"
    compile 'com.android.support:palette-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:design:22.2.0'
    compile 'se.emilsjolander:stickylistheaders:2.1.2'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
    compile 'com.squareup.retrofit:retrofit:1.5.0'
    compile 'com.timehop.stickyheadersrecyclerview:library:0.4.0@aar'
    compile 'com.jakewharton:butterknife:7.0.0'
    compile 'com.squareup:otto:1.3.8'
    compile files('libs/picasso-2.1.1.jar')
    compile files('libs/jsoup-1.7.3.jar')
    compile files('libs/slf4j-android-1.5.8.jar')
    //compile project(':Libraries:ViewPagerIndicator:library')
}

task showVersion << {
    println buildscript.configurations.classpath.resolvedConfiguration.firstLevelModuleDependencies.moduleVersion
}

crashlytics.verboseGradlePlugin = true

clean.dependsOn showVersion
